grammar com.cohesionforce.hla.dsl.Fom hidden(WS, ML_COMMENT, SL_COMMENT, COMMENT)

generate fom "http://www.cohesionforce.com/hla/dsl/Fom"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.cohesionforce.com/hla/dsl/Omt" as omt

Fed:
	OPEN 'FED'
		federation = Federation
		version = FEDversion
		OPEN 'spaces' spaces+=Space* CLOSE
		OPEN 'objects' 
			OPEN 'class' 'ObjectRoot'
				OPEN 'attribute' rootAttribute=ValidID properties=AttributeProperties CLOSE
				OPEN 'class' 'RTIprivate' CLOSE
				classes+=AttributeClass*
			CLOSE
		CLOSE
		OPEN 'interactions' 
			OPEN 'class' 'InteractionRoot' properties=AttributeProperties
				OPEN 'class' 'RTIprivate' properties=AttributeProperties CLOSE
				classes+=InteractionClass*
			CLOSE
		CLOSE
	CLOSE
;

Federation:
	OPEN 'Federation' name=(ValidID|TEXT) CLOSE
;

FEDversion:
	OPEN 'FEDversion' version=TEXT CLOSE
;

Space:
	OPEN 'space' name=ValidID
		dimensions+=Dimension* 
	CLOSE
;

Dimension:
	OPEN 'dimension' name=ValidID CLOSE
;

AttributeClass:
	OPEN 'class' ref=[omt::AttributeClass|ValidID]
		attributes+=Attribute*
		classes+=AttributeClass*
	CLOSE
;

Attribute:
	OPEN 'attribute' ref=[omt::Attribute|ValidID]  properties=AttributeProperties CLOSE
;

AttributeProperties:
	transport=('reliable'|'best_effort') order=('timestamp'|'receive') space=[Space]?
;
InteractionClass:
	OPEN 'class' ref=[omt::Interaction|ValidID] properties=AttributeProperties
		parameters+=Parameter*
		classes+=InteractionClass*
	CLOSE
;

Parameter:
	OPEN 'parameter' name=ValidID CLOSE
;

ValidID:
	ID | 'Federation' | 'class' | 'attribute' | 'dimension' | 'space'
;

terminal OPEN		: '(';
terminal CLOSE		: ')';
terminal COMMENT 	: ';;' !('\n'|'\r')* ('\r'? '\n')?;
terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal INT returns ecore::EInt: ('0'..'9')+;

terminal STRING	: 
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
		; 
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;
terminal TEXT		: !('"'|"'"|'('|')'|' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;