grammar com.cohesionforce.hla.dsl.Fom hidden(WS, ML_COMMENT, SL_COMMENT, COMMENT)

generate fom "http://www.cohesionforce.com/hla/dsl/Fom"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Fed:
	'(' 'FED'
		federation = Federation
		version = FEDversion
		'(' 'spaces' spaces+=Space* ')'
		'(' 'objects' classes+=AttributeClass* ')'
		'(' 'interactions' classes+=InteractionClass* ')'
	')'
;

Federation:
	'(' 'Federation' name=(ValidID|TEXT) ')'
;

FEDversion:
	'(' 'FEDversion' version=TEXT ')'
;

Space:
	'(' 'space' name=ValidID
		dimensions+=Dimension* 
	')'
;

Dimension:
	'(' 'dimension' name=ValidID ')'
;

AttributeClass:
	'(' 'class' name=ValidID 
		attributes+=Attribute*
		classes+=AttributeClass*
	')'
;

Attribute:
	'(' 'attribute' name=ValidID transport=('reliable'|'best_effort') order=('timestamp'|'receive') space=[Space]? ')'
;

InteractionClass:
	'(' 'class' name=ValidID transport=('reliable'|'best_effort') order=('timestamp'|'receive') space=[Space]?
		parameters+=Parameter*
		classes+=InteractionClass*
	')'
;

Parameter:
	'(' 'parameter' name=ValidID ')'
;

ValidID:
	ID | 'Federation' | 'class' | 'attribute' | 'dimension' | 'space'
;

terminal COMMENT 	: ';;' !('\n'|'\r')* ('\r'? '\n')?;
terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal INT returns ecore::EInt: ('0'..'9')+;

terminal STRING	: 
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
		; 
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;
terminal TEXT		: !('"'|"'"|'('|')'|' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;