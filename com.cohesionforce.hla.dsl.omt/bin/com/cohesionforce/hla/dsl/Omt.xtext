grammar com.cohesionforce.hla.dsl.Omt  hidden(WS, ML_COMMENT, SL_COMMENT, COMMENT)

generate omt "http://www.cohesionforce.com/hla/dsl/Omt"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

HLAOMT_DIF_v1_3:
	OPEN 'DIF'
		name=AnyText version=AnyText 
		OPEN 'TYPE' type=AnyText CLOSE 
	CLOSE
	objectModels+=ObjectModel+
;

ObjectModel:
	OPEN 'ObjectModel' 
		(OPEN 'Name' name=STRING CLOSE)
		(OPEN 'VersionNumber' version=STRING CLOSE)
		(OPEN 'Type' type=MOD_Type CLOSE)?
		(OPEN 'Purpose' purpose=STRING CLOSE)?
		(OPEN 'ApplicationDomain' domain=STRING CLOSE)?
		(OPEN 'SponsorOrgName' sponsor=STRING CLOSE)?
		(OPEN 'POCHonorificName' pocHonorific=STRING CLOSE)?
		(OPEN 'POCFirstName' pocFirstName=STRING CLOSE)?
		(OPEN 'POCLastName' pocLastName=STRING CLOSE)?
		(OPEN 'POCOrgName' pocOrgName=STRING CLOSE)?
		(OPEN 'POCPhone' pocPhone=STRING CLOSE)?
		(OPEN 'POCEmail' pocEmail=STRING CLOSE)?
		(OPEN 'ModificationDate' modificationDate=TEXT CLOSE)?
		(OPEN 'MOMVersion' momVersion=STRING CLOSE)?
		timeRepresentation=TimeRepresentation?
		lookAhead=Lookahead?
		omtComponents+=OMTComponent*
	CLOSE
;

TimeRepresentation:
	OPEN 'TimeRepresentation'
		OPEN 'Units' units=STRING unitsNote=NoteRef? CLOSE
		OPEN 'Minimum' min=STRING minNote=NoteRef? CLOSE
		OPEN 'Maximum' max=STRING maxNote=NoteRef? CLOSE
		OPEN 'InitialValue' initial=STRING initialNote=NoteRef? CLOSE
		OPEN 'DataType' dataType=STRING typeNote=NoteRef? CLOSE
		(OPEN 'Operators' operators=STRING operatorsNote=NoteRef? CLOSE )?
	CLOSE
;

Lookahead:
	OPEN 'Lookahead'
		OPEN 'Units' units=STRING unitsNote=NoteRef? CLOSE
		OPEN 'DataType' dataType=STRING typeNote=NoteRef? CLOSE
		(OPEN 'AdditionOperator' operator=STRING operatorNote=NoteRef? CLOSE)?
	CLOSE
;

MOD_Type:
	'FOM' | 'SOM' | 'OTHER'
;

OMTComponent:
	Class | EnumeratedDataType | ComplexDataType | RoutingSpace | Interaction | Note
;

RoutingSpace:
	OPEN 'RoutingSpace' OPEN 'Name' name=STRING note=NoteRef? CLOSE
		dimensions+=Dimension*
	CLOSE
;

Dimension:
	OPEN 'Dimension' OPEN 'Name' name=STRING nameNote=NoteRef? CLOSE
		OPEN 'DimensionType' (type=[TypeReference|STRING]|dataType=BASE_DATA_TYPE) typeNote=NoteRef? CLOSE
		domain=DimensionDomain
		( OPEN 'RangeSetUnits' units=STRING unitsNote=NoteRef? CLOSE )?
		OPEN 'NormalizationFunction' function=STRING functionNote=NoteRef? CLOSE
	CLOSE
;	

DimensionDomain: 
	DimensionRange | DimensionSet
;

DimensionRange:
	OPEN 'DimensionMinimum' minimum=STRING note=NoteRef? CLOSE
	(OPEN 'DimensionMaximum' maximum=STRING CLOSE)?
	OPEN 'IntervalType' IntervalType CLOSE
;

IntervalType:
	'Open' | 'Closed'
;

DimensionSet:
	OPEN 'DimensionSet' members+=DimensionSetMember+ note=NoteRef? CLOSE
;

DimensionSetMember:
	OPEN 'Member' value=STRING CLOSE
;

Class:
	OPEN 'Class' OPEN 'ID' id=INT CLOSE
		OPEN 'Name' name=STRING note=NoteRef? CLOSE
		(OPEN 'MOMClass' isMOMClass=IsMOMClass CLOSE)?
		OPEN 'PSCapabilities' PSCapabilities CLOSE
		description=Description?
		
		components+=ClassComponent* CLOSE
;

IsMOMClass:
	'TRUE' | 'FALSE'
;

PSCapabilities: 'P' | 'S' | 'PS' | 'N';

ClassComponent:
	Attribute | SuperClass
;

SuperClass:
	OPEN 'SuperClass' superClass=INT CLOSE
;

Attribute:
	OPEN 'Attribute' OPEN 'Name' name=STRING nameNote=NoteRef? CLOSE
		dataType=DataType?
		cardinality=Cardinality?
		units=Units?
		resolution=Resolution?
		accuracy=Accuracy?
		accuracyCondition=AccuracyCondition?
		(OPEN 'UpdateType' updateType=ATT_UpdateType updateTypeNote=NoteRef? CLOSE)?
		(OPEN 'UpdateCondition' updateCondition=STRING updateConditionNote=NoteRef? CLOSE)?
		(OPEN 'TransferAccept' transferAccept=ATT_TransferAccept transferAcceptNote=NoteRef? CLOSE)?
		(OPEN 'UpdateReflect' updateReflect=ATT_UpdateReflect updateReflectNote=NoteRef? CLOSE)?
		description=Description?
		(OPEN 'RoutingSpace' routingSpace=STRING CLOSE)? //TODO - LINK
 	CLOSE
;

ATT_UpdateType: 'Static' | 'Periodic' | 'Conditional';
ATT_TransferAccept: 'T' | 'A' | 'TA' | 'N';
ATT_UpdateReflect: 'U' | 'R' | 'UR';

EnumeratedDataType:
	OPEN 'EnumeratedDataType' OPEN 'Name' name=STRING note=NoteRef? CLOSE
		(OPEN 'MOMEnumeratedDataType'  isMOMEnumeratedDataType=('TRUE'|'FALSE') CLOSE)?
		literals+=Enumeration+ 
	CLOSE
;

Enumeration:
	OPEN 'Enumeration' OPEN 'Enumerator' literal=STRING enumeratorNote=NoteRef? CLOSE
		OPEN 'Representation' value=INT representationNote=NoteRef? CLOSE 
	CLOSE
;

ComplexDataType: 
	OPEN 'ComplexDataType' OPEN 'Name' name=STRING note=NoteRef? CLOSE
		(OPEN 'MOMComplexDataType' isMOMComplexDataType=('TRUE'|'FALSE') CLOSE)?
		components+=ComplexComponent+
	CLOSE
;

ComplexComponent: 
	OPEN 'ComplexComponent'
		position=Position?
		fieldName=FieldName
		dataType=DataType?
		cardinality=Cardinality?
		units=Units?
		resolution=Resolution?
		accuracy=Accuracy?
		accuracyCondition=AccuracyCondition?
	CLOSE
;
Accuracy:
	OPEN 'Accuracy' accuracy=STRING accuracyNote=NoteRef? CLOSE
;
AccuracyCondition:
	OPEN 'AccuracyCondition' accuracyCondition=STRING accuracyConditionNote=NoteRef? CLOSE
;
Resolution:
	OPEN 'Resolution' resolution=STRING resolutionNote=NoteRef? CLOSE
;
Position:
	OPEN 'Position' position=STRING CLOSE
;
DataType:
	OPEN 'DataType' (refType=[TypeReference|STRING]| dataType=BASE_DATA_TYPE) dataTypeNode=NoteRef? CLOSE
//	OPEN 'DataType' dataType=STRING dataTypeNode=NoteRef? CLOSE
;
TypeReference:
	ComplexDataType | EnumeratedDataType
;
FieldName:
	OPEN 'FieldName' name=AnyString fieldNameNote=NoteRef? CLOSE
;
Units:
	OPEN 'Units' units=AnyString unitsNote=NoteRef? CLOSE
;
Cardinality:
	OPEN 'Cardinality' cardinality=STRING cardinalityNote=NoteRef? CLOSE
;

Interaction:
	OPEN 'Interaction' OPEN 'ID' id=INT CLOSE
		OPEN 'Name' name=STRING nameNote=NoteRef? CLOSE
		OPEN 'ISRType' type=ISRType typeNote=NoteRef? CLOSE
		(OPEN 'MOMInteraction' isMOMInteraction=('TRUE'|'FALSE') CLOSE)?
		description+=Description
		(OPEN 'RoutingSpace' routingSpace=[RoutingSpace|STRING] CLOSE)?
		components+=InteractionComponent*
	CLOSE
;

ISRType: 
	'I' | 'S' | 'R' | 'IS' | 'IR' | 'N';

InteractionComponent:
	Parameter | SuperInteraction
;

SuperInteraction:
	OPEN 'SuperInteraction' ID=INT CLOSE
;

Parameter:
	OPEN 'Parameter' OPEN 'Name' name=STRING nameNote=NoteRef? CLOSE
		dataType=DataType?
		cardinality=Cardinality?
		units=Units?
		resolution=Resolution?
		accuracy=Accuracy?
		accuracyCondition=AccuracyCondition?
		description=Description?
	CLOSE
;

Description:
	OPEN 'Description' description=STRING CLOSE
;

Note:
	OPEN 'Note' 
		OPEN 'NoteNumber' ID=INT CLOSE
		OPEN 'NoteText' text=STRING CLOSE
	CLOSE
;


NoteRef : '[' refs+=INT (',' refs+=INT)* ']';

AnyText:
	ID | TEXT
;

AnyString:
	STRING | BASE_DATA_TYPE
;

terminal BASE_DATA_TYPE : '"' ('unsigned short' | 'short' | 'unsigned long' | 'long' | 'unsigned long long' 
	| 'long long' | 'double' | 'float' | 'boolean' | 'any' | 'string' |	'char' | 'octet') '"';

terminal OPEN		: '(';
terminal CLOSE		: ')';
terminal COMMENT 	: ';;' !('\n'|'\r')* ('\r'? '\n')?;
terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal INT returns ecore::EInt: ('0'..'9')+;

terminal STRING	: 
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
		; 
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;
terminal TEXT		: !(','|'['|']'|'"'|"'"|'('|')'|' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;